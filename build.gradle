group = 'org.meiskalt7.service'

rootProject.wrapper.gradleVersion = '4.6'
version = file('version.txt').exists() ? file('version.txt').text.trim() : "1.0.0"

buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
        servicePackage = 'org.meiskalt7.filtrator'
        servicePackageSlashes = servicePackage.replace('.', '/')

        ver = [
                springBoot          : '1.5.10.RELEASE',
                dependencyManagement: '1.0.4.RELEASE',
                sonarqube           : '2.6.2',
                lombok              : '3.6.4'
        ]
    }

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
                "io.spring.gradle:dependency-management-plugin:$ver.dependencyManagement",
                "org.springframework.boot:spring-boot-gradle-plugin:$ver.springBoot",
                "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:$ver.sonarqube",
                "io.freefair.gradle:lombok-plugin:$ver.lombok"
    }
}

ext {
    defaultEncoding = 'UTF-8'

    releaseName = System.getenv("releaseName") ?: System.getProperty("releaseName") ?: "0"
    branch = System.getenv("branch") ?: System.getProperty("branch") ?: "0"
    revision = System.getenv("buildRev") ?: System.getProperty("buildRev") ?: ""
}

allprojects {
    repositories {
        rootProject.buildscript.repositories.each {
            repositories.add(it)
        }
    }

    apply plugin: 'jacoco'
}

apply plugin: 'idea'
apply plugin: 'org.sonarqube'

subprojects {

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: "io.freefair.lombok"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    compileJava {
        options.encoding = defaultEncoding
        options.compilerArgs = ['-parameters', '-Xlint:unchecked']
    }

    compileTestJava {
        options.encoding = defaultEncoding
        options.compilerArgs = ['-parameters', '-Xlint:unchecked']
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    tasks.withType(Javadoc) {
        options.encoding = defaultEncoding
        options.memberLevel = JavadocMemberLevel.PRIVATE
        options.addBooleanOption("Xdoclint:all").setValue(true)
    }

    test {

        systemProperty 'spring.profiles.active', 'test'
        systemProperty 'user.language', 'ru'
        systemProperty 'user.country', 'RU'

        jacoco {
            excludes = [
                    servicePackageSlashes + '/**/BizTalkSource.*',
                    servicePackageSlashes + '/**/*AutoConfiguration.*',
                    servicePackageSlashes + '/api/Api.*',
                    servicePackageSlashes + '/ServiceApplication.*',
            ]
        }

        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
        reports {
            junitXml.enabled true
            html.enabled false
        }

        failFast = System.getProperty('failFast') != null
        maxParallelForks = 1
        //forkEvery = 1
        include servicePackageSlashes + '/**/*Test.class'
//        minHeapSize = "1600m"
//        maxHeapSize = "2g"

        testLogging {
            events 'skipped', 'failed'
            exceptionFormat = 'full'

            debug {
                events "started", "passed", "skipped", "failed"
            }

            info {
                events "passed", "skipped", "failed"
            }
        }

    }

}

sonarqube {
    properties {
        property 'sonar.sourceEncoding', defaultEncoding
        property 'sonar.java.source property', '1.8'
        property 'sonar.junit.reportPaths', "$buildDir/test-results/test"
        property 'sonar.jacoco.reportPaths', "$buildDir/jacoco/test.exec"
        properties['sonar.coverage.exclusions'] = '**/' + servicePackageSlashes + '/service/biztalk/**/*BizTalkSource.*, ' +
                '**/' + servicePackageSlashes + '/**/*AutoConfiguration.*, ' +
                '**/' + servicePackageSlashes + '/api/Api.*, ' +
                '**/' + servicePackageSlashes + '/ServiceApplication.*'
    }
}

idea {

    if (parent == null) {
        project {
            languageLevel = '1.8'
            vcs = 'Git'
        }
    }

    module {
        excludeDirs += file('.idea')
    }
}